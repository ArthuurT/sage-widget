#viewLinePlot(?numberOfPage,?numberOfArticles)

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix wd: <http://www.wikidata.org/entity/>

SELECT ?numberOfPage (COUNT(DISTINCT ?article) AS ?numberOfArticles) WHERE {
  ?article wdt:P31 ?class .
  VALUES(?class) { (wd:Q13442814) }
  ?article wdt:P1104 ?numberOfPage .
}
GROUP BY ?numberOfPage

---------------------------

#viewBarPlot(?countryLabel,?population)

prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix wd: <http://www.wikidata.org/entity/>

SELECT ?countryLabel (MAX(?population) AS ?population) WHERE {
  ?country wdt:P47 wd:Q1009 .  # shares border with Cameroon
  ?country <http://schema.org/name> ?countryLabel .
  ?country wdt:P1082 ?population .
  FILTER (LANG(?countryLabel) = "fr")
}
GROUP BY ?countryLabel

----------------------------

#viewMap(?lat,?long)

PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
  
SELECT DISTINCT ?lat ?long WHERE { 
  ?actor a dbo:Actor .
  ?actor dbo:birthPlace ?place .
  ?place geo:lat ?lat .
  ?place geo:long ?long .
  ?place dbo:country dbr:Canada . 
}